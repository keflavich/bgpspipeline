 function planck2,ww,t,microns=microns
;+ 
; ROUTINE:     planck
;
; USEAGE:      result=planck(ww,t [,/microns])
;
; INPUT:
;
;   ww         wavenumber (cm-1) or wavelength (microns) if /microns set,
;              can be either scalar or vector
;
;   t          temperature (degrees K), can be scalar or vector.
;
; OUTPUT:
;
;   result     black body spectral intensity, commonly known as the 
;              Planck function
;
;                               microns set         microns not set
;                               -----------         ---------------
;                units:        W/m2/sr/micron         W/m2/sr/cm-1
;
;              
;
; UNIT CONVERSION FACTORS:
;
;               W/m2/sr/cm-1    x 1000 = erg/s/cm2/sr/cm-1
;               W/m2/sr/micron  x 1000 = erg/s/cm2/sr/micron
;               W/m2/sr/micron         = erg/s/cm2/sr/nm
;
; the spectral flux generated by a black body surface is =   pi * planck
;
;; EXAMPLE
; 
;; plot black body curve for a 300, 280 and 260 degrees K.
;
; w=findrng(10000/333.33,10000/4.,200)
; plot,w,planck(w,300),xtitle='wavenumber (cm!a-1!n)',ymargin=[10,2],$
;        ytitle='W/m!a2!n/sr/cm!a-1!n',title='Black Body Radiance'
; AXIS,0,4*!d.y_ch_size,XAX=0,/dev,xtickv=[0,10000./[50,20,13,10,8,6,5,4]],$
;       xticks=8,xtickn=[' ','50','20','13','10','8','6','5','4'],$
;       xtitle='wavelength (microns)'
; 
; oplot,w,planck(w,280),li=3
; oplot,w,planck(w,260),li=4
; legend,'.cBlack Body\.cTemperature\\300\280\260',li=[0,3,4],pos=[.7,.6,.9,.9]
;
;; planck function for one of the sbdart standard atmospheres
;
; atmosz,idatm=4,zz,pp,tt
; !p.multi=[0,2,2] & yt='Altititude (km)' & xt='W/m!a2!n/sr'
; plot,planck(3,tt,/mic),zz,title='!7k!x = 3um',ytit=yt
; plot,planck(4,tt,/mic),zz,title='!7k!x = 4um'
; plot,planck(5,tt,/mic),zz,title='!7k!x = 5um',ytit=yt,xtit=xt
; plot,planck(15,tt,/mic),zz,title='!7k!x = 15um',xtit=xt
;
;
;  author:  Paul Ricchiazzi                            3dec92
;           Institute for Computational Earth System Science
;           University of California, Santa Barbara
;-
h=6.6262e-27 & bk=1.3807e-16 & c=2.9979e10
if keyword_set(microns) then begin 
  c1=2.e13*h*c^2
  c2=1.e4*h*c/bk
  arg=c2/(float(ww)*float(t))
  arg=arg < 70.
  return,c1/(float(ww)^5*(exp(arg)-1.))
endif else begin
  c1=2.e-3*h*c^2
  c2=h*c/bk
  arg=c2*float(ww)/float(t)
  return,c1*float(ww)^3/(exp(arg)-1.)  
endelse
end

