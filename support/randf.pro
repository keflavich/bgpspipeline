 function randf,n,slope,periodic=periodic
;+
; routine:      randf
; 
; purpose:      generate random time history data with given power spectrum
;
; useage:       randf,n,slope
;
; inputs:
;   slope       power law slope of power spectrum (default=4)
;
;   n           number of elements in time history. Since an fft is used
;               to compute the random numbers powers of 2 are most efficient.
;
; EXAMPLE:      
;               plot,randf(256,2)
;
; method:       a power law is used to set the spectral power as a
;               function of frequency (i.e., p=1/w^s).  After the phase of
;               the fourier components are randomized, the temporal
;               realization is generated by taking the inverse FFT.
;               
;
;  author:  Paul Ricchiazzi                            27oct93
;           Institute for Computational Earth System Science
;           University of California, Santa Barbara
;-
w=[indgen((n+1)/2),reverse(1+indgen(n/2))]

if keyword_set(periodic) then begin
  r=randomu(iseed,1+n/2)
  r=[r(0:(n-1)/2),reverse(r(1:*))] 
endif else begin
  r=randomu(iseed,n)
endelse

power=1./(w>1.)^slope
gg=sqrt(power)
gg=gg*exp(complex(0,2*!pi*r)) ; randomized phase

return,float(fft(gg,1))                        ; realization
end



